plugins {
    id 'com.android.application'
    id 'realm-android'
}

android {

    signingConfigs {
        config {
            storeFile file("debug.keystore")
        }
    }

    // ANDROID X
    // compileSdkVersion 27
    compileSdkVersion 29

    defaultConfig {
        applicationId "com.example.easydid"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug{
            ext.enableCrashlytics = false
            //ext.alwaysUpdateBuildId = false
        }

        release {
            minifyEnabled = false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
    }

    buildFeatures{
        viewBinding true
        dataBinding true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    flavorDimensions "stage"
    productFlavors{
        dev{
            dimension "stage"
            versionCode 100
            minSdkVersion 21

            aaptOptions.cruncherEnabled = false
            resConfigs "ko", "mdpi"
        }

        prd{
            dimension "stage"
            versionCode new Date().format('yyMMddHHmm').toInteger()
        }
    }
    splits{
        abi{
            enable true
            reset()
            include 'x86', 'x86_64', 'armeabi-v7a', 'arm64-v8a'
            universalApk false
        }
    }

    android.variantFilter { variant ->
        def buildName = variant.buildType.name;
        def flavorName = variant.getFlavors().get(0).name;

        if (flavorName == 'dev' && buildName == 'release'
                || flavorName == 'prd' && buildName == 'debug') {
            variant.setIgnore(true);
        }

        splits.abi.enable = (flavorName == 'prd');
    }
}

dependencies {

    implementation 'androidx.appcompat:appcompat:1.3.0'
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'androidx.preference:preference:1.1.1'
    implementation 'io.reactivex.rxjava3:rxandroid:3.0.0'
    implementation 'io.reactivex.rxjava3:rxjava:3.0.13'
    implementation 'com.github.kizitonwose.colorpreference:core:1.1.0'
    implementation 'com.github.kizitonwose.colorpreference:support:1.1.0'
    implementation project(path: ':videodownload')
//    implementation ('io.socket:socket.io-client:1.0.1') {
//        exclude group: 'org.json', module: 'json'
//    }
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
}